plugins {
    id 'org.springframework.boot' version '2.6.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id "org.openapi.generator" version "5.3.1"
    id 'java'
}

group = 'com.examples.apifirst'
version = '1.0.0-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'io.springfox:springfox-swagger2:3.0.0'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'
    runtimeOnly 'com.h2database:h2'
    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
}

// OpenAPI generator settings
openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/specs/spec.yaml"
    outputDir = "$buildDir/generated/openapi"
    apiPackage = "com.examples.apifirst.api"
    modelPackage = "com.examples.apifirst.model"
    configOptions = [
            dateLibrary: "java8",
            delegatePattern: "true"
    ]
}

// Add generated code to classpath
sourceSets {
    main {
        java.srcDirs += "$buildDir/generated/openapi/src/main/java"
    }
}

compileJava {
    dependsOn(':openApiGenerate')
}

springBoot {
    mainClass = 'com.example.stoplight.StoplightApplication'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
